
; Vaquero core

; def

(def x 5)
(sys.test 'def (= x 5))


; quote

(sys.test 'quote-symbol (= (quote foo) 'foo))
(sys.test 'quote-list (= (list 1 2 3) '(1 2 3)))


; if

(sys.test 'if-true
    (if true
        true
        false))

(sys.test 'if-false
    (if false
        false
        true))


; seq

(sys.test 'seq-1
    (= (seq 1) 1))

(sys.test 'seq-2
    (= (seq 1 2) 2))

(sys.test 'seq-3
    (= (seq 1 2 3) 3))


; proc

(sys.test 'proc-identity
    (let ()
        (def foo (proc (x) x))
        (= 'zebra (foo 'zebra))))

(sys.test 'proc-add
    (let ()
        (def bar (proc (x y) (+ x y)))
        (= 5 (bar 2 3))))

(sys.test 'proc-recursion
    (let ()
        (def fact
            (proc (n)
                (if (= n 1)
                    1
                    (* n (fact (- n 1))))))
        (= 120 (fact 5))))

(sys.test 'proc-options
    (let ()
        (def zuul
            (proc (x)
                (if opt.keymaster
                    (if opt.gatekeeper
                        (list x opt.keymaster opt.gatekeeper)
                        (list x opt.keymaster))
                    x)))
        (and
            (= (zuul 'gozer) 'gozer)
            (= (zuul 'gozer keymaster: 'louis) '(gozer louis))
            (= (zuul 'gozer keymaster: 'louis gatekeeper: 'dana) '(gozer louis dana)))))

(sys.test 'proc-rest
    (let ()
        (def embiggen
            (proc (xs)
                (if xs.empty?
                    xs
                    (pair (* 10 xs.head) (embiggen xs.tail)))))
        (def various
            (proc (funk)
                (funk rest)))
        (= (various embiggen 1 2 3) '(10 20 30))))

(sys.test 'proc-mutual-recursion
    (let ()
        (def F
            (proc (n)
                (if (= n 0)
                    1
                    (- n (M (F (- n 1)))))))
        (def M
            (proc (n)
                (if (= n 0)
                    0
                    (- n (F (M (- n 1)))))))
        (and (= (F 19) 12) (= (M 19) 12)))) 

(sys.test 'proc-lexical-closure
    (let ()
        (def counter
            (proc (x)
                (proc ()
                    (if opt.inc
                        (x.set! 0 (+ x.0 opt.inc))
                        x.0))))
        (def c1 (counter (vector 0)))
        (def c2 (counter (vector 23)))
        (c1 inc: 1)
        (c2 inc: 7)
        (and (= (c1) 1)
             (= (c2) 30)
             ((proc ()
                (def x 99)
                (c1 inc: 7)
                (and (= x 99)
                     (= (c1) 8)))))))


; continuations - return, gate, and capture

(sys.test 'return
    (= 10
        ((proc ()
            (return 10)
            20
            30))))

(sys.test 'return-nested
    (= 5
        ((proc ()
            (def x ((proc () (return 10) 20)))
            (if (= x 10)
                (return 5)
                6)))))

(sys.test 'gate-no-capture
    (= 3
        (gate (+ 1 2))))

(sys.test 'gate-abort
    (= 2
        (gate
            (+ 1
                (capture k 2)))))

(sys.test 'capture-call-1
    (= 3
        (gate
            (+ 1
                (capture k (+ 1 (k 1)))))))

(sys.test 'capture-call-n
    (= 5
        (gate
            (+ 1
                (capture k (+ 1 (k (k (k 1)))))))))


; errors - ensure, guard, and fail

(proc proc-handler (err kontinue)
    (if (= err 'resume)
        (kontinue 42)
        (if (= err 'default)
            69
            (fail 'we-are-fucked))))

(sys.test 'guard-no-error
    (= 6
        (+ 1
            (guard
                proc-handler
                (+ 2 3)))))

(sys.test 'guard-default
    (= 70
        (+ 1
            (guard
                proc-handler
                (+ 2 (fail 'default))))))

(sys.test 'guard-continue
    (= 45
        (+ 1
            (guard
                proc-handler
                (+ 2 (fail 'resume))))))

(sys.test 'guard-abort
    (= 0
        (guard
            (proc (e k) 0)
            (+ 1
                (guard
                    proc-handler
                    (+ 2 (fail 'crap)))))))

(sys.test 'ensure-no-jump
    ((proc ()
        (def x (vector 1))
        (def y
            (ensure
                (proc () (x.set! 0 3))
                (seq (x.set! 0 2) (+ x.0 3))))
        (and (= x.0 3) (= y 5)))))

(sys.test 'ensure-no-capture-across
    ((proc ()
        (def x (vector 1))
        (def y
            (gate
                (* 10
                    (ensure
                        (proc () (x.set! 0 3))
                        (seq
                            (x.set! 0 5)
                            (+ 2
                                (capture k (k (k x.0)))))))))
        (and (= x.0 3) (= y 90)))))

(sys.test 'ensure-error
    ((proc ()
        (def x (vector 1))
        (def y
            (guard
                (proc (e k) (+ 7 e))
                (ensure
                    (proc () (x.set! 0 3))
                    (seq
                        (x.set! 0 5)
                        (+ 2 (fail x.0))))))
        (and (= x.0 3) (= y 12)))))
                
(sys.test 'let
   (let (x 7 y 13)
      (and
         (= '(2 . 3)
            (let (x 2 y 3)
               (pair x y)))
         (= x 7)
         (= y 13))))

(sys.test 'wall
    (let (x 1 y 2 z 3)
        (wall (x (+ x y z) y (* y z)) ; new environment with passed parameters
            (def got-x (= x 6))
            (def got-y (= y 6))
            (def no-z  (not (env.has? 'z)))
            (and got-x got-y no-z))))

(sys.test 'unicode
    (seq
        (def λא象
            (proc () "λא象"))
        (= (λא象) "λא象")))

